plugins {
  id 'application'
  id 'checkstyle'
  id 'pmd'
  id 'jacoco'
  id 'io.spring.dependency-management' version '1.0.11.RELEASE'
  id 'org.springframework.boot' version '2.5.7'
  id 'org.owasp.dependencycheck' version '6.5.0.1'
  id 'com.github.ben-manes.versions' version '0.39.0'
  id 'se.patrikerdes.use-latest-versions' version '0.2.18'
  id 'org.sonarqube' version '3.3'
  id 'au.com.dius.pact' version '4.3.2'
}

ext['rest-assured.version'] = '4.0.0'

group = 'uk.gov.hmcts.reform'
version = '1.0.2'

sourceCompatibility = 11
targetCompatibility = 11

allprojects {
  apply plugin: 'checkstyle'
  apply plugin: 'com.github.ben-manes.versions'
  apply plugin: 'org.owasp.dependencycheck'

  sourceSets {
    functionalTest {
      java {
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
        srcDir file('src/functionalTest/java')
      }
      resources.srcDir file('src/functionalTest/resources')
    }

    integrationTest {
      java {
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
        srcDir file('src/integrationTest/java')
      }
      resources.srcDir file('src/integrationTest/resources')
    }

    contractTest {
      java {
        compileClasspath += main.output
        runtimeClasspath += main.output
        srcDir file('src/contractTest/java')
      }
      resources.srcDir file('src/contractTest/resources')
    }
  }

  tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation" << "-Werror"
  }

  task functional(type: Test, description: 'Runs the functional tests.', group: 'Verification') {
    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
  }

  task integration(type: Test, description: 'Runs the integration tests.', group: 'Verification') {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
  }

  task smoke(type: Test) {
    description = "Runs Smoke Tests"
    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath

    useJUnit {
      includeCategories 'uk.gov.hmcts.reform.pdf.service.SmokeTest'
    }
  }

  task contract(type: Test) {
    description = 'Runs the consumer Pact tests'
    useJUnitPlatform()
    testClassesDirs = sourceSets.contractTest.output.classesDirs
    classpath = sourceSets.contractTest.runtimeClasspath
    systemProperty 'pact.rootDir', "pacts"
  }


  checkstyle {
    maxWarnings = 0
    toolVersion = '8.29'
    // need to set configDir to rootDir otherwise submodule will use submodule/config/checkstyle
    getConfigDirectory().set(new File(rootDir, 'config/checkstyle'))
  }

  jacocoTestReport {
    executionData(test, integration)
    reports {
      xml.enabled = true
      csv.enabled = false
      xml.setDestination(file("${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml"))
    }
  }

  def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { version.toUpperCase().contains(it) }
    def regex = /^[0-9,\\.v\\-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
  }

  dependencyUpdates {
    rejectVersionIf {
      isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)
    }
  }

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
  dependencyCheck {
    // Specifies if the build should be failed if a CVSS score above a specified level is identified.
    // range of 0-10 fails the build, anything greater and it doesn't fail the build
    failBuildOnCVSS = System.getProperty('dependencyCheck.failBuild') == 'true' ? 0 : 11
    suppressionFile = 'config/owasp/suppressions.xml'
    analyzers {
      // Disable scanning of .NET related binaries
      assemblyEnabled = false
    }
  }

  repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
    maven { url 'https://jitpack.io' }
  }

// it is important to specify logback classic and core packages explicitly as libraries like spring boot
// enforces it's own (older) version which is not recommended.

  test {
    useJUnitPlatform()
    failFast = true
  }

  def flyingSaucerVersion = '9.1.21'

  dependencyManagement {
    imports {
      mavenBom "org.xhtmlrenderer:flying-saucer-pdf:${flyingSaucerVersion}"
    }
  }

  ext {
    junit = '5.8.2'
    reformLogging = '5.1.9'
    springfoxSwagger = '3.0.0'
    flyingSaucer = flyingSaucerVersion
    openPdf = dependencyManagement.importedProperties['openpdf.version']
  }

}

project.tasks['sonarqube'].dependsOn test, integration, jacocoTestReport
project.tasks['check'].dependsOn integration

pmd {
  toolVersion = "6.11.0"
  sourceSets = [sourceSets.main, sourceSets.test, sourceSets.functionalTest, sourceSets.integrationTest]
  ruleSets = []
  ruleSetFiles = files("config/pmd/ruleset.xml")
}

sonarqube {
  properties {
    property "sonar.projectName", "Reform :: RPE PDF Service"
    property "sonar.projectKey", "uk.gov.hmcts.reform:rpe-pdf-service"
    property "sonar.coverage.jacoco.xmlReportPaths", "${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml"
  }
}

configurations.all {
  resolutionStrategy {
    eachDependency { DependencyResolveDetails details ->
      if (details.requested.group in ['com.fasterxml.jackson.core', 'com.fasterxml.jackson.module', 'com.fasterxml.jackson.datatype']) {
        details.useVersion '2.12.2'
      }

      if (details.requested.name == 'jackson-databind') {
        details.useVersion '2.12.2'
      }
    }
  }
}

ext {
    log4JVersion = '2.15.0'
    pactVersion = '4.3.1'
}

dependencies {
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-json'

  implementation group: 'io.springfox', name: 'springfox-swagger2', version: springfoxSwagger
  implementation group: 'io.springfox', name: 'springfox-swagger-ui', version: springfoxSwagger

  implementation group: 'com.google.guava', name: 'guava', version: '31.0.1-jre'

  implementation group: 'com.github.hmcts.java-logging', name: 'logging', version: reformLogging
  implementation group: 'com.github.hmcts.java-logging', name: 'logging-appinsights', version: reformLogging

  implementation group: 'com.github.hmcts', name: 'reform-api-standards', version: '0.5.2'


  implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: log4JVersion
  implementation group: 'org.apache.logging.log4j', name: 'log4j-to-slf4j', version: log4JVersion

  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: junit
  testImplementation("org.junit.vintage:junit-vintage-engine") {
    exclude group: "org.hamcrest", module: "hamcrest-core"
  }
  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: junit
  testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: junit

  testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test'
  testImplementation group: 'org.pdfbox', name: 'com.springsource.org.pdfbox', version: '0.7.3'


  integrationTestImplementation sourceSets.main.runtimeClasspath
  integrationTestImplementation sourceSets.test.runtimeClasspath

  functionalTestImplementation sourceSets.main.runtimeClasspath
  functionalTestImplementation sourceSets.test.runtimeClasspath
  contractTestImplementation sourceSets.main.runtimeClasspath
  contractTestImplementation sourceSets.test.runtimeClasspath
  functionalTestImplementation group: 'io.rest-assured', name: 'rest-assured'

  contractTestImplementation group: 'au.com.dius.pact.provider', name: 'junit5', version: pactVersion
  contractTestImplementation group: 'au.com.dius.pact.provider', name: 'spring', version: pactVersion
  contractTestImplementation group: 'au.com.dius.pact.provider', name: 'junit5spring', version: pactVersion
  contractTestImplementation("org.junit.jupiter:junit-jupiter-api:5.8.2")
  contractTestRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.8.2")
  contractTestImplementation('org.junit.jupiter:junit-jupiter-api:5.8.2')

  implementation project(':pdf-generator')
}


mainClassName = 'uk.gov.hmcts.reform.pdf.service.PDFServiceApplication'

bootJar {
  archiveFileName = 'pdf-service-all.jar'

  manifest {
    attributes('Implementation-Version': project.version.toString())
  }
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}


task runProviderPactVerification(type: Test) {
  logger.lifecycle("Runs pact Tests")
  testClassesDirs = sourceSets.contractTest.output.classesDirs
  classpath = sourceSets.contractTest.runtimeClasspath
}

runProviderPactVerification.dependsOn contract

runProviderPactVerification.finalizedBy pactVerify

